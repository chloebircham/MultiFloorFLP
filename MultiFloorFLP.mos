model basic2
  options noimplicit
  uses "mmxprs", "mmsystem" ,"mmxnlp"
  ! uses "mminsight" ! uncomment this line for an Xpress Insight model

  declarations
    CeilingHeight: integer  !Height of each floor in the building
    Floors: integer         !Number of Floors in the building
    Departments: integer    !Number of Departments needing to be placed
    Elevators: integer      !Number of elevators on each floor
    EWidth: real            !The set width of each elevator
    EHeight: real           !The set height of each elevator
    FWidth: real            !The Width of each floor in the whole facility
    FHeight: real           !The Height of each floor in the whole facility
    beta:integer            !parameter for aspect ratio
    datafile: string        !file name for the input data
    tstart:real             !Run time start 
    tend:real               !Run time end
    runtime:real            !Total run time
  end-declarations


  datafile:= "example1.dat"

  initialisations from datafile
    Floors
    Departments
    Elevators
    CeilingHeight
    EWidth
    EHeight
    FWidth
    FHeight
    beta
  end-initialisations

declarations
  DArea: array(1..Departments) of real                 !Area requirements for each department
  Cost: array(1..Departments,1..Departments) of real   !Interconnecting costs
end-declarations

initialisations from datafile
  DArea
  Cost
end-initialisations


  declarations
    zik: array(1..Departments+Elevators, 1..Floors) of mpvar                    !Array for floor allocation for each department+elevator
    Zij: array(1..Departments+Elevators, 1..Departments+Elevators) of mpvar     !Array for departments+elevators located on the same floor
    Xij: array(1..Departments+Elevators, 1..Departments+Elevators) of mpvar     !Array for placement of departments+elevators in respect to each other
    Yij: array(1..Departments+Elevators, 1..Departments+Elevators) of mpvar     !Array for placement of departments+elevators in respect to each other
    x: array(1..Departments+Elevators) of mpvar                                 !Centre x coordinate for each department and elevator
    y: array(1..Departments+Elevators) of mpvar                                 !Centre y coordinate for each department and elevator
    dv:array(1..Departments,1..Departments) of mpvar                            !vertical distance between departments
    dh:array(1..Departments,1..Departments) of mpvar                            !horizontal distance on same floor
    de:array(1..Departments,1..Departments) of mpvar                            !horizontal distance on different floors
    DWidth: array(1..Departments+Elevators) of mpvar                            !Width of each department
    DHeight: array(1..Departments+Elevators) of mpvar                           !Height of each department
    cost: linctr                                                                !Objective function
    M: real                                                                     !Sufficiently large M for horizontal distance calculations
    fnum:real                                                                   !For formatting output

    !Variables used for calculating absolute values in distance calculation constraints!
    tv:array(1..Departments,1..Departments) of mpvar
    tx:array(1..Departments,1..Departments) of mpvar
    ty:array(1..Departments,1..Departments) of mpvar
    te1:array(1..Departments,1..Departments) of mpvar
    te2:array(1..Departments,1..Departments) of mpvar
    te3:array(1..Departments,1..Departments) of mpvar
    te4:array(1..Departments,1..Departments) of mpvar
  end-declarations

tstart:=gettime

!Objective Function: horizontal + vertical distance travelled between every pair of departments multiples by cost matrix

forall(i in 1..Departments, j in 1..Departments)do
if(i<j)then
cost +=  Cost(i,j)*(dv(i,j)+de(i,j))
end-if
end-do


!Setting the binary variable restrictions
forall(i,j in 1..Departments+Elevators)do
Xij(i,j) is_binary
Yij(i,j) is_binary
Zij(i,j) is_binary
forall(k in 1..Floors) zik(i,k) is_binary
end-do




!Setting the elevator dimensions to match the input requirements for elevator indices in the array
forall(l in 1+Departments..Departments+Elevators)do
DWidth(l)=EWidth
DHeight(l)=EHeight
end-do




!Sufficiently large M for distance calculations
M:= FWidth+FHeight+(Floors*CeilingHeight)

!Each Department is only allocated to one floor
!zik=1 if department i located on floor k, zik=0 otherwise
forall(i in 1..Departments) do
sum(k in 1..Floors) zik(i,k)=1
end-do


!!!Distances!!!
!tv constraints: absolute value of the difference in floor number
!tx constraints: absolute value, difference in x coordinates
!ty constraints: absolute value, difference in y coordinates
!te1..te4 constraints: absolute value, difference in distances from departments to elevator, then elevator to department.
!dh: horizontal distance for departments on same floors (distance between coordinates of each dept.)
forall(i,j in 1..Departments) do
if (i <j) then

!Vertical distance
dv(i,j)= CeilingHeight* tv(i,j)
tv(i,j)>= sum(k in 1..Floors) k*(zik(i,k)-zik(j,k))
tv(i,j)>= -sum(k in 1..Floors) k*(zik(i,k)-zik(j,k))


!horixontal distance for departments if they're on the same floor
dh(i,j)= tx(i,j) + ty(i,j)
tx(i,j)>= x(i) - x(j)
tx(i,j)>= x(j) - x(i)
ty(i,j)>= y(i) - y(j)
ty(i,j)>= y(j) - y(i)

!Ensuring de is atleast as large as departments located on the same floor.
de(i,j)>=dh(i,j)


!If there are multiple floors
!de calculates the distance via furthest elevator, if the departments are on different floors.
if (Elevators>0) then
de(i,j)>= te1(i,j) + te2(i,j) + te3(i,j) + te4(i,j) - M*Zij(i,j)
forall( l in 1..Elevators) do
te1(i,j)>= x(i)-x(l+Departments)
te1(i,j)>= x(l+Departments)-x(i)
te2(i,j)>= y(i)-y(l+Departments)
te2(i,j)>= y(l+Departments)-y(i)
te3(i,j)>= x(j)-x(l+Departments)
te3(i,j)>= x(l+Departments)-x(j)
te4(i,j)>= y(j)-y(l+Departments)
te4(i,j)>= y(l+Departments)-y(j)
end-do
end-if

end-if
end-do


!Setting Zij=1 if departments are on the same floor
forall(i,j in 1..Departments, k in 1..Floors)do
if(i<j)then
Zij(i,j) >= zik(i,k) + zik(j,k) -1
Zij(i,j) <= 1 - zik(i,k) + zik(j,k)
Zij(i,j) <= 1 + zik(i,k) - zik(j,k)
end-if
end-do


!Constraints to determine placement and prevent overlapping 
forall(i,j in 1..Departments+Elevators)do
if(i<j) then
x(i)-x(j) >= 0.5*(DWidth(i) + DWidth(j)) - FWidth*(1-Zij(i,j) + Xij(i,j) + Yij(i,j))
x(j)-x(i) >= 0.5*(DWidth(i) + DWidth(j)) - FWidth*(2- Zij(i,j)-Xij(i,j) + Yij(i,j))
y(i)-y(j) >= 0.5*(DHeight(i) + DHeight(j)) - FHeight*(2-Zij(i,j) +Xij(i,j) - Yij(i,j))
y(j)-y(i) >= 0.5*(DHeight(i) + DHeight(j)) - FHeight*(3-Zij(i,j)-Xij(i,j) - Yij(i,j))

end-if
end-do


!Ensures each department fits inside the facility, e.g centre point + half width muct be contained in the area of one floor.
forall(i in 1..Departments+Elevators) do
x(i)+0.5*DWidth(i) <= 0.5*FWidth
x(i)-0.5*DWidth(i) >= -0.5*FWidth
y(i)+0.5*DHeight(i)<= 0.5*FHeight
y(i)-0.5*DHeight(i) >= -0.5*FHeight

!Setting ranges for x and y coordinates
-0.5*FWidth<=x(i)
x(i)<=0.5*FWidth
-0.5*FHeight<=y(i)
y(i)<=0.5*FHeight
end-do





forall(i in 1..Departments) do

!Ensuring height and width satisfy conditions for department areas
DWidth(i) * DHeight(i) >= DArea(i)

!Shaping constraints, short and fat/ long and skinny. E.g. beta=1 sets conditions for perfect squares
DWidth(i)- beta*DHeight(i)<=0
DHeight(i)- beta*DWidth(i)<=0

end-do




!Ensuring that every elevator covers all floors
forall(i in 1+Departments..Departments+Elevators, k in 1..Floors) do
zik(i,k)=1
end-do

!Ensuring that all elevators share every floor with all other elevators
!And every department shares its floor with all elevators
forall(i in 1..Departments+Elevators, j in 1+Departments..Departments+Elevators) do
if (i<j) then

Zij(i,j)=1
end-if
end-do




minimise(cost)

tend:= gettime
runtime:= tend-tstart
case getprobstat of
    XPRS_OPT: writeln("Problem status: optimum ")
    XPRS_UNF: writeln("Problem status: unfinished")
    XPRS_INF: writeln("Problem status: inf")
    XPRS_UNB: writeln("Problem status: unbounded")
    XPRS_OTH: writeln("Problem status: failed")
    else writeln("Problem status:? ")
  end-case
setparam('xnlp_verbose', true)
  writeln("runtime is: ", runtime)



  writeln("The Floors the departments are allocated to and their centrepoints:")
  writeln(strfmt("Dep",3), strfmt("F",3), strfmt("      (x,y)",3))
  forall(d in 1..Departments)do


  forall(j in 1..Floors)do
  if (getsol(zik(d,j))=1) then
  fnum:=j
  end-if
  end-do




 writeln(strfmt(d,3), strfmt(fnum,3), strfmt("(",3), strfmt(getsol(x(d)),2,3), ",", strfmt(getsol(y(d)),2,3), strfmt(")",-3))
 end-do

writeln("")
writeln("With the following Dimensions:")
writeln("")
  writeln("Department       Width x Height")

  forall(d in 1..Departments) do
  writeln(strfmt(d,3),"              ", strfmt(getsol(DWidth(d)),2,2)," x  ", strfmt(getsol(DHeight(d)),2,2))
  end-do

forall(e in 1..Elevators)do
writeln("Centre Point of Elevator ", e, ": (", strfmt(getsol(x(Departments+e)),2,2), ",", strfmt(getsol(y(Departments+e)),2,2), ").")
end-do


  writeln("The solution has a cost value of: ", strfmt(getobjval,2,2))




end-model